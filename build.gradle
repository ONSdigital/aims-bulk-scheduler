import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id "io.freefair.lombok" version "8.11"
	id 'java'
    id "com.github.ben-manes.versions" version "0.51.0"
}

group = 'uk.gov.ons'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	mockitoAgent
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudGcpVersion', "5.9.0")
	set('springCloudVersion', "2024.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'com.google.cloud:spring-cloud-gcp-dependencies:5.9.0'
	implementation 'com.google.cloud:spring-cloud-gcp-starter-pubsub'
	implementation 'com.google.cloud:spring-cloud-gcp-starter-bigquery'
	implementation 'com.google.cloud:spring-cloud-gcp-starter-sql-mysql'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	compileOnly 'org.projectlombok:lombok:1.18.36'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:4.2.0'
	testImplementation 'com.playtika.testcontainers:embedded-google-pubsub:3.1.9'
	testImplementation 'org.mockito:mockito-core:5.14.2'
	mockitoAgent 'org.mockito:mockito-core:5.14.2'
}

configurations.mockitoAgent {
    transitive = false
}

dependencyManagement {
	imports {
		mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:${springCloudGcpVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
    useJUnitPlatform()

    doFirst {
        jvmArgs("-javaagent:${configurations.mockitoAgent.singleFile}")
    }

    OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;

    if (os.isLinux()) {
        def uid = ["id", "-u"].execute().text.trim()
        environment "DOCKER_HOST", "unix:///run/user/$uid/podman/podman.sock"
    } else if (os.isMacOsX()) {
        environment "DOCKER_HOST", "unix:///Users/${System.getenv('USER')}/.colima/default/docker.sock"
    }
    environment "TESTCONTAINERS_RYUK_DISABLED", "true"

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

//    if (project.hasProperty('excludeTests')) {
//        exclude project.property('excludeTests')
//        exclude '*bootstrap.properties'
//    }
}
